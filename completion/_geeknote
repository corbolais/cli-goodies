#compdef geeknote
#autoload

local -a _1st_arguments
_1st_arguments=(
  'tag-edit:Edit/rename tag.'
  'show:Output note in the terminal.'
  'notebook-list:Show the list of existing notebooks in your Evernote.'
  'tag-list:Show the list of existing tags in your Evernote.'
  'logout:Logout from Evernote.'
  'user:Show information about active user.'
  'find:Search notes in Evernote.'
  'settings:Show and edit current settings.'
  'edit:Edit note in Evernote.'
  'create:Create note in evernote.'
  'remove:Remove note from Evernote.'
  'notebook-create:Create new notebook.'
  'login:Authorize in Evernote.'
  'notebook-edit:Edit/rename notebook.'
  'tag-create:Create new tag.'
)

local expl

_arguments \
  '*:: :->subcmds' && return 0


if (( CURRENT == 1 )); then
  _describe -t commands "geeknote subcommand" _1st_arguments
  return 0
fi

case "$words[1]" in
  settings)
    _arguments \
      '(--editor)--editor[set the default editor]' \
      ;;
  create)
    _arguments \
      '(-t --title)'{-t,--title}'[title of the new note]' \
      '(-c --content)'{-c,--content}'[content of the new note]' \
      '(-tg --tags)'{-tg,-tags}'[comma separated tags of the new note]' \
      '(-nb --notebook)'{-nb,--notebook}'[notebook for the new note]' \
      ;;
  edit)
    _arguments \
      '(-n --note)'{-n,--note}'[title of the note to edit]' \
      '(-t --title)'{-t,--title}'[title of the new note]' \
      '(-c -content)'{-c,--content}'[content of the new note]' \
      '(-tg --tags)'{-tg,--tags}'[comma separated tags of the new note]' \
      '(-nb --notebook)'{-nb,--notebook}'[notebook for the new note]' \
      ;;
  find)
    _arguments \
      '(-s --search)'{-s,--search}'[text to find]' \
      '(-tg --tags)'{-tg,--tags}'[list of tags that notes should have]' \
      '(-nb --notebooks)'{-nb,--notebooks}'[list on notebooks where to make search]' \
      '(-d --date)'{-d,--date}'[data ro data range]' \
      '(-cn --count)'{-cn,--count}'[how many results to show]' \
      '(-ee --exact-entry)'{-ee,--exact-entry}'[search must match exactly]' \
      '(-cs --content-search)'{-cs,--content-search}'[search the content of the note]' \
      "(-wu --with-url)"{-wu,--with-url}"[adds a direct url of each note in the results to Evernote's web-version]" \
      ;;
  remove)
    _arguments \
      '(-n --note)'{-n,--note}'[name of the note you want to delete]' \
      '(-f --force)'{-f,--force}'[do not ask for confirmation]' \
      ;;
  notebook-edit)
    _arguments \
      '(--notebook)--notebook[name of notebook to rename]' \
      '(-t --title)'{-t,--title}'[new name of notebook]' \
      ;;
  tag-create)
    _arguments \
      '(-t --title)'{-t,--title}'[name of new tag]' \
      ;;
  tag-edit)
    _arguments \
      '(-tgn --tagname)'{-tgn,--tagname}'[name of tag to rename]' \
      '(-t --title)'{-t,--title}'[new name for tag]' \
      ;;
esac


